apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "im.windsing"
        minSdkVersion 17
        targetSdkVersion 26
        // use the version code
        versionCode rootProject.ext.versionCodeProp
        versionName rootProject.ext.versionNameProp

        // custom values
        resValue "string", "bug_report_url",  "https://riot.im/bugreports/submit"
        ndk {
                abiFilters "armeabi", "armeabi-v7a"
        }
        multiDexEnabled true
        flavorDimensions ""
    }

    signingConfigs {
        release {
            storeFile file(project.hasProperty("RELEASE_STORE_FILE") ? RELEASE_STORE_FILE : "/dummy/path")
            storePassword project.hasProperty("RELEASE_STORE_PASSWORD") ? RELEASE_STORE_PASSWORD : "dummy password"
            keyAlias project.hasProperty("RELEASE_KEY_ALIAS") ? RELEASE_KEY_ALIAS : "dummy alias"
            keyPassword project.hasProperty("RELEASE_KEY_PASSWORD") ? RELEASE_KEY_PASSWORD : "dummy password"
        }
    }

    buildTypes {
        debug {
            resValue "string", "git_revision", "\"${gitRevision()}\""
            resValue "string", "git_revision_unix_date", "\"${gitRevisionUnixDate()}\""
            resValue "string", "git_revision_date", "\"${gitRevisionDate()}\""
            minifyEnabled false
        }

        release {
            resValue "string", "git_revision", "\"${gitRevision()}\""
            resValue "string", "git_revision_unix_date", "\"${gitRevisionUnixDate()}\""
            resValue "string", "git_revision_date", "\"${gitRevisionDate()}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        matrixorg.initWith(buildTypes.release)
        matrixorg {
            signingConfig signingConfigs.release
        }

        dexOptions {
            incremental true
            javaMaxHeapSize "4g"
        }
    }

    productFlavors {
        app {
            applicationId "im.windsing.alpha"
            // use the version name
            versionCode rootProject.ext.versionCodeProp
            versionName rootProject.ext.versionNameProp
            resValue "string", "allow_gcm_use", "true"
            resValue "string", "allow_ga_use", "true"
            resValue "string", "flavor_description", ""
        }

        appfdroid {
            applicationId "im.windsing.alpha"
            // use the version name
            versionCode rootProject.ext.versionCodeProp
            versionName rootProject.ext.versionNameProp
            resValue "string", "allow_gcm_use", "false"
            resValue "string", "allow_ga_use", "true"
            resValue "string", "flavor_description", "FDroid"
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        disable 'MissingTranslation'

    }

    repositories {
        flatDir {
            dir 'libs'
        }

        maven {
            url "https://s3.amazonaws.com/repo.commonsware.com"
        }

        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
}

def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}

def gitRevisionUnixDate() {
    def cmd = "git show -s --format=%ct HEAD^{commit}"
    return cmd.execute().text.trim()
}

def gitRevisionDate() {
    def cmd = "git show -s --format=%ci HEAD^{commit}"
    return cmd.execute().text.trim()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'me.leolin:ShortcutBadger:1.1.2@aar'

    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'

    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'io.pristine:libjingle:9690@aar'

    compile 'pl.droidsonroids.gif:android-gif-drawable:1.2.+'

    compile 'com.googlecode.libphonenumber:libphonenumber:8.0.1'

    compile 'com.github.guanpy:eventbus:1.0.0'

    /************* Matrix SDK management **************/
    // use the matrix SDK as external lib
    compile(name: 'windsing-sdk', ext: 'aar')
    compile(name: 'olm-sdk', ext: 'aar')
    compile(name: 'library-release', ext: 'aar')
    compile(name: 'wblib-release', ext: 'aar')
    compile files('libs/afinal_0.5.1_bin.jar')
    compile files('libs/jsoup-1.8.1.jar')
    compile files('libs/zxing_core_3.0.1.jar')
    compile "com.badlogicgames.gdx:gdx-backend-android:1.6.1"

    // use the matrix SDK as a part of the project
    // you have to uncomment some lines in settings.gradle
    //compile project(':matrix-sdk')

    /************* flavors management **************/

    // app flavor only
    appCompile 'com.google.android.gms:play-services-gcm:8.1.0'
    appCompile 'com.google.android.gms:play-services-analytics:8.1.0'

    //for pic search
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile "io.reactivex:rxjava:1.2.5"
    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.android.support:multidex:1.0.1'
    // fdroid flavor only
}

